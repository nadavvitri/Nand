// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   Mux16 (a=x, b[0..15]=false, sel=zx, out=tempx);  // if (zx == 1) set x = 0
   Not16 (in=tempx, out=c);           // compute !x
   Mux16 (a=tempx, b=c, sel=nx, out=temp2x);        // if (nx == 1) set x = !x
   
   Mux16 (a=y, b[0..15]=false, sel=zy, out=tempy);  // if (zy == 1) set y = 0
   Not16 (in=tempy, out=d);           // compute !y
   Mux16 (a=tempy, b=d, sel=ny, out=temp2y);       // if (ny == 1) set y = !y
   
   Add16 (a=temp2x, b=temp2y, out=xplusy);   // compute x + y
   And16 (a=temp2x, b=temp2y, out=xandy);    // compute x & y
   Mux16 (a=xandy, b=xplusy, sel=f, out=tempout);  // if (f == 1)  set out = x + y, else out = x & y
   
   Not16 (in=tempout, out=nout);     // compute !out
   Mux16 (a=tempout, b=nout, sel=no, out=out, out[15]=sign, out[0..7]=part1, out[8..15]=part2);  // if (no == 1) set out = !out
   
   // check if out == 0 or not 
   Or8Way (in=part1, out=temp1);
   Or8Way (in=part2, out=temp2);
   Or (a=temp1, b=temp2, out=temp3);
   
   // if (out == 0) set zr = 1
   Not (in=temp3, out=zr);
   
   // if (out < 0) set ng = 1
   Mux (a=false, b=true, sel=sign, out=ng);
}